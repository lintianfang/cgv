#version 460

//***** begin interface of shape2d.glsl ***********************************
uniform bool origin_upper_left = false;
vec4 transform_world_to_window_space(vec2 p);
//***** end interface of shape2d.glsl ***********************************

const vec2 corners[4] = vec2[](
	vec2(0.0, 0.0),
	vec2(1.0, 0.0),
	vec2(0.0, 1.0),
	vec2(1.0, 1.0)
);

struct vertex {
	vec4 position_size;
	vec4 texcoords;
};

layout (std430, binding = 0) readonly buffer data_buffer {
	vertex in_data[];
};

uniform vec2 position_offset;
uniform vec2 alignment_offset_factors;
uniform vec2 text_size;
uniform float font_size = 32.0;
uniform float angle;

//uniform bool align_to_pixels;

out vec2 tex_coord;

mat2 rotate(in float angle) {
	float c = cos(angle);
	float s = sin(angle);
	mat2 M = mat2(
		c, s,
		-s, c
	);
	return M;
}

void main() {
	int id = gl_VertexID;
	vec2 corner = corners[id];

	vertex v = in_data[gl_BaseInstance + gl_InstanceID];
	v.position_size *= font_size * text_size.y;

	mat2 R = rotate(radians(angle));

	tex_coord = vec2(mix(v.texcoords.xy, v.texcoords.zw, corner));

	if(origin_upper_left)
		corner.y = 1.0 - corner.y;

	vec2 final_position = v.position_size.xy;
	vec2 alignment_factors = alignment_offset_factors;
	if(origin_upper_left) {
		final_position.y = -(final_position.y + v.position_size.w);
		alignment_factors.y = -alignment_factors.y;
	}

	// offset local glyph position by text alignment
	final_position += text_size * font_size * alignment_factors;
	
	// rotate local glyph position	
	final_position = R * final_position;
	
	// move glyph to global position
	final_position += position_offset;

	vec2 size = v.position_size.zw;
	
	// transform to pixel coordinates
	corner *= size;

	// rotate
	corner = R * corner;

	// offset
	corner += final_position;

	//if(align_to_pixels)
	//	corner = round(corner);
	
	gl_Position = transform_world_to_window_space(corner);
}
